
Application note

Strange behavior of series underdamped Josephson junctions

Consider the following circuit:

    * Generated by Xic from file foo_test
    b1 2 1 6 jjx area=.1
    b2 1 0 5 jjx area=.1
    r2 3 2 37
    v1 3 0 pulse(-1.6m 31m 50p 75p 75p 125p 400p)
    .model jjx JJ(RTYPE=1, CCT=1, VG=2.8MV, DELV=.100mV,
    + R0=33.6, RN=1.68, ICRIT=1000uA, CAP=4.5PF,icfact=0.6)
    .tran 1p 10n
    .plot v(3) v(2)
    .plot v(5,6)

This circuit is intended to clamp the voltage at v(2), however after
about 4ns the resetting becomes erratic.  Strangely, the voltages and
phases of the two series junctions diverge.

One might initially question how two identical devices in series would
evolve differently in time.  It is true that numerical error plays a
part, however the effect is real, and would manifest itself in an
actual circuit.  This inability to reset is termed "punch through".

The junctions are strongly hysteretic.  This implies that the plasma
resonance of the native junctions is highly underdamped.  Consider the
two junctions as two RLC resonant circuits.  If the resonators are
oscillating in phase, the sum voltage v(2) is also oscillating, and
therefor generating current in the bias resistor r2.  The resistor r2
(37 ohms) consequently damps the in-phase resonance.

The out of phase resonance is quite different.  In this case, the sum
voltage v(2) is not oscillating (the two resonators cancel) thus the
bias resistance does not damp this resonance mode.  The only damping
is supplied by the model R0 resistance, which is nearly ten times
larger.  Thus, the out of phase resonance has about one tenth of the
damping of the in phase resonance.

The out of phase resonance will alter the relative switching points of
the two junctions as a function of time, unlike the in phase
resonance.  Thus, in the array, if the out of phase resonance mode is
activated, the switching points of the two junctions will differ,
depending upon the relative phases of the two junctions at the time of
the current step.  This difference in time translates into a larger
difference in phase when the junctions reset, which "pumps" the out of
phase mode.

At it happens, tiny numerical inaccuracies are responsible for
generating a small out of phase component, which is magnified at every
switching event, until the amplitude is so large that the resetting
becomes erratic.  The time between clock pulses is not large enough to
allow the out of phase resonance to die away.  One would expect
similar and perhaps worse results in a physical circuit, as thermal
noise and geometric imbalance will pump the out of phase mode.

The only way around the problem is to reduce the clock frequency,
and/or increase the damping across *each* junction.

Note that in Spice, if one uses rectangular integration by setting

    .options maxord=1

the problem seems far less severe.  However, this is illusory, as the
error introduced by the lower integration order tends to act as
additional damping.  If the time step is also reduced, the problem
reappears.

